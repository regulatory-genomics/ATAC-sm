
##### global workflow dependencies #####
conda: "envs/global.yaml"
# ===================================================
#                    Setup Section
# ===================================================
# libraries
import os
import sys
import shutil

import pandas as pd
import yaml
from peppy import Project
from snakemake.utils import validate, min_version
from string import Template

##### set minimum snakemake version #####
min_version("8.20.1")

module_name = "atacseq_pipeline"
report: os.path.join("report", "workflow.rst")
# list of names of the used environment specifications in workflow/envs/{env_name}.yaml
envs = ["macs2_homer","multiqc","pybedtools","uropa","ggplot","ataqv"]

# load config and sample annotation sheets #####
configfile: os.path.join("config","config.yaml")
validate(config, schema="../schemas/config.schema.yml")
# load sample/unit annotation
annot = pd.read_csv(config["project"]["samples"])
validate(annot, schema="../schemas/samples.schema.yml")

# Create a unique identifier for each run: sample_name_run
annot['sample_run'] = annot['sample_name'].astype(str) + '_' + annot['run'].astype(str)

duplicates = annot[annot.duplicated(subset=['sample_run'], keep=False)]

if not duplicates.empty:
    sys.stderr.write("\nError: Duplicate (sample_name + run) pairs found in sample sheet!\n")
    sys.stderr.write("The following rows are not unique:\n")
    sys.stderr.write(str(duplicates['sample_run']) + "\n\n")
    sys.exit(1)

annot = annot.set_index('sample_run')

if 'read_type' not in annot.columns:
    annot['read_type'] = config["project"]["read_type"]

# Get unique sample names (for downstream analysis)
samples = annot.reset_index().drop_duplicates(subset='sample_name', keep='first').set_index("sample_name").to_dict(orient="index")

##### set global variables
result_path = os.path.join(config["project"]["out_dir"], module_name)
HOMER_path = os.path.abspath(os.path.join("resources", config["project"]["name"], "HOMER"))

# to deal with rule ambiguity concerning final outputs in counts/
ruleorder: sample_annotation > quantify_aggregate
ruleorder: merge_bam > peak_calling

# ===================================================
#                    Rules Section
# ===================================================
rule all:
    input:
        # PROCESSING
        multiqc_report = os.path.join(result_path,"report","multiqc_report.html"),
        # QUANTIFICATION
        sample_annotation = os.path.join(result_path, "counts", "sample_annotation.csv"),
        sample_annotation_plot = os.path.join(result_path, "report", "sample_annotation.png"),
        support_counts = os.path.join(result_path,"counts","support_counts.csv"),
        consensus_counts = os.path.join(result_path,"counts","consensus_counts.csv"),
        promoter_counts = os.path.join(result_path,"counts","promoter_counts.csv"),
        tss_counts = os.path.join(result_path,"counts","TSS_counts.csv"),
        HOMER_knownMotifs = os.path.join(result_path,"counts","HOMER_knownMotifs.csv"),
        # ANNOTATION
        consensus_annotation = os.path.join(result_path,'counts',"consensus_annotation.csv"),
        # QC - ATAQV
        ataqv_report = os.path.join(result_path, "ataqv_report"),
        # ALIGNMENT STATS REPORT
        align_stats_report = os.path.join(result_path, "report", "align_stats_report.tsv"),
        # EXPORT environments and configurations
        envs = expand(os.path.join(result_path,'envs','{env}.yaml'),env=envs),
        configs = os.path.join(result_path,'configs','{}_config.yaml'.format(config["project"]["name"])),
        annotations = os.path.join(result_path,'configs','{}_annot.csv'.format(config["project"]["name"])),
        merged_peaks = os.path.join(result_path,"summary","peaks","merged_peaks.bed"),
    resources:
        mem_mb=config["resources"]["mem_mb"],
    threads: config["resources"]["threads"]
    log:
        os.path.join("logs","rules","all.log")

##### load rules #####
include: os.path.join("rules", "export.smk")
include: os.path.join("rules", "common.smk")
include: os.path.join("rules", "resources.smk")
include: os.path.join("rules", "genome_prepare.smk")
include: os.path.join("rules", "processing.smk")
include: os.path.join("rules", "report.smk")
include: os.path.join("rules", "quantification.smk")
include: os.path.join("rules", "region_annotation.smk")
include: os.path.join("rules", "qc.smk")
